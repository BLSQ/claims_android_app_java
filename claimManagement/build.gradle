import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

apply plugin: 'com.android.application'

static def getDate() {
    LocalDateTime date = LocalDateTime.now()
    DateTimeFormatter formatter = DateTimeFormatter.ofPattern('dd MMMM yyyy HH:mm').withLocale(Locale.UK)
    return date.format(formatter)
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.openimis.imisclaims"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 3
        versionName "2.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        useLibrary 'org.apache.http.legacy'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    flavorDimensions 'std'

    productFlavors.all {
        resValue "string", "app_name_claims", "openIMIS Claims"
        resValue "string", "ReleaseDateValue", getDate()
    }

    productFlavors {
        demoProd {
            applicationIdSuffix ".demoProd"
            resValue "string", "app_name_claims", "Claims Demo"
            dimension 'std'
        }

        demoRelease {
            applicationIdSuffix ".demoRelease"
            resValue "string", "app_name_claims", "Claims Release"
            dimension 'std'
        }

        chfDev {
            applicationIdSuffix ".chfDev"
            resValue "string", "app_name_claims", "Claims CHF"
            dimension 'std'
        }
        mvDev {
            applicationIdSuffix ".mvDev"
            resValue "string", "app_name_claims", "Claims MV"
            dimension 'std'
        }
        bephaDev {
            applicationIdSuffix ".bephaDev"
            resValue "string", "app_name_claims", "Claims BEPHA"
            dimension 'std'
        }
        local {
            applicationIdSuffix ".local"
            resValue "string", "app_name_claims", "Claims Local"
            dimension 'std'
        }
    }

    sourceSets {
        demoProd.java.srcDir 'src/demoProd/java'
        demoProd.res.srcDir 'src/demoProd/res'

        demoRelease.java.srcDir 'src/demoRelease/java'
        demoRelease.res.srcDir 'src/demoRelease/res'

        chfDev.java.srcDir 'src/chfDev/java'
        chfDev.res.srcDir 'src/chfDev/res'

        mvDev.java.srcDir 'src/mvDev/java'
        mvDev.res.srcDir 'src/mvDev/res'

        bephaDev.java.srcDir 'src/bephaDev/java'
        bephaDev.res.srcDir 'src/bephaDev/res'

        local.java.srcDir 'src/local/java'
        local.res.srcDir 'src/local/res'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'

    }
}

dependencies {
    //implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(path: ':General')
    // https://mvnrepository.com/artifact/com.squareup.picasso/picasso
    implementation group: 'com.squareup.picasso', name: 'picasso', version: '2.5.2'
    implementation project(':CaptureActivity')
    // https://mvnrepository.com/artifact/net.lingala.zip4j/zip4j
    implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '1.2.7'
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpcore
    //implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.1'
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
//    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.4.1'
    implementation "cz.msebera.android:httpclient:4.4.1.2"

}
